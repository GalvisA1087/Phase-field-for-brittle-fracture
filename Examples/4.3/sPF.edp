// ================================================================================
// ================================================================================
// ================================================================================
// PARAMETERS
// ================================================================================

include "Parameters.edp"

// ================================================================================
// ================================================================================
// ================================================================================
// SOME VARIABLES
// ================================================================================

include "Variables.edp"

// ================================================================================
// ================================================================================
// ================================================================================
// MESH
// ================================================================================

include "Mesh.edp"

// ================================================================================
// ================================================================================
// ================================================================================
// FE-SPACE
// ================================================================================

include "FEspace.edp"

// ================================================================================
// ================================================================================
// ================================================================================
// VARIATIONAL PROBLEMS
// ================================================================================	
	
include "Variational.edp"
	
// ================================================================================
// ================================================================================
// ================================================================================
// SOME FUNCTIONS
// ================================================================================

include "Functions.edp"

include "Mesh_Adaptation.edp"

include "FieldToExport.edp"

include "ExportVTU.edp"

include "PlotFreeFEM.edp"

include "ExportFile.edp"

// ================================================================================
// ================================================================================
// MEASURING TIME
// ================================================================================
time=clock();
Totaltime=0;
tsolve=0;
tsolveud=0;
// ================================================================================
// ================================================================================

cout << " " << endl; 
cout << " ========================================================================= " << endl;
cout << " ========================================================================= " << endl;
cout << " ================== standard PHASE FIELD simulator ======================= " << endl;
cout << " ========================================================================= " << endl;
cout << " Dr. Andres Galvis 														" << endl;
cout << " School of Electrical and Mechanical Engineering							" << endl;			 
cout << " University of Portsmouth - UK 											" << endl;
cout << " October 2025 																" << endl;
cout << " ========================================================================= " << endl;
cout << " ========================================================================= " << endl;
cout << " " << endl; 

for (int i = 0; i < Nsteps; i++){ // from the second step load

	tsolve=clock();
	// ================================================================================
	// ================================================================================
	// ================================================================================

	cout << " " << endl; 
	cout << " Step = " << i+1 << ",  u_2 (dimensionless) = " << alpha << ",  u_2 = " << alpha*curlU*1000 << " mm" <<endl;
	cout << " " << endl; 

	// ==========================================
	// load increment ===========================	
	
	if (i<=300 || i>800){
		alpha += du;
	}	
	if (i>300 && i<=800){
		alpha -= du;
	}
		
	tsolveud=clock();
	
	// =============================================================
	// Computing the displacement problem ==========================		
	cout << " " << endl; 
	cout << "        Solving the coupled displacement-phase field problem" << endl; 
						
	real[int] Ft=rhsu0(0,Vh);
	real[int] Fd=rhsd0(0,Xh);
	real[int] F = [Ft,Fd];		
	Nitr=0;	errordNR=1.0; erroruNR=1.0;
	while ( (erroruNR > Tol) || (errordNR > Tol) ){
			
		matrix K00;
		if (i<=600 || i>1000){
			K00=Displacement0(Vh,Vh);
		}
		if (i>600 && i<=1000){	
			K00=Displacementn(Vh,Vh);
		}
		matrix K11=Phasefield0(Xh,Xh);			
		real[int] xk = [u1[],d[]];				 
		matrix Kt = [[K00,0], [0,K11]];	
		set(Kt,solver="SPARSESOLVER",tgv=1e30); // MUMPS can be also used here		
	    real[int] Xaux = Kt*xk;	
	    real[int] R = F - Xaux;	    		
		real[int] dxkp1 = Kt^-1 * R;
		u1[] += dxkp1(0:Vh.ndof-1);
		d[] += dxkp1(Vh.ndof:Vh.ndof+Xh.ndof-1);		
		[erroruNR,errordNR] = computError(dxkp1);
						
		Nitr += 1;
		cout << "     	  	" << " Error uNR: " << erroruNR << " Error dNR: " << errordNR << " Iteration: " << Nitr << endl;			    
	}	
		
	tsolveud = clock()-tsolveud;
	
	// ===============================================================================
	// Mesh adaptation at every step =================================================
		
	cout << " " << endl;
	cout << "    Mesh adaptation" << endl;		
		
	Th = MeshAdaptation(Th,u1[],d[]);
	d=d; [u1,u2]=[u1,u2]; Hold=Hold; 
	
	// =============================================================
	// Computing strains and stresses ==============================
			
	cout << " " << endl;
	cout << "    Computing strains and stresses" << endl;
		
	eps11 = dx(u1); 
	eps22 = dy(u2) ; 
	eps12 = (dy(u1)+dx(u2))/2 ;
	trepsp = ( (eps11+eps22)/2.0 + abs(eps11+eps22)/2.0 );
	En = (eps11+eps22)/2.0;
	e11 = eps11 - En; 
	e22 = eps22 - En;	
	e12 = eps12;

	sigma11 = (1-d)^2 * ( lambda*( eps11 + eps22 ) + 2*mu*eps11 );
	sigma22 = (1-d)^2 * ( lambda*( eps11 + eps22 ) + 2*mu*eps22 );		
	sigma12 = (1-d)^2 * ( 2*mu*eps12 );
	
	// =============================================================
	// Strain energy and mixed criterion ===========================		
	// New energy		
	cout << " " << endl;
	cout << "    Checking irreversibility" << endl;
			
	Xh Hnew = 0.5*trepsp^2 + mu*(e11*e11 + 2*e12*e12 + e22*e22);				
	H = max(Hnew,Hold);	

	// =============================================================
	// Computing the force to export ==============================	
	
	cout << " " << endl;
	cout << "    Computing the test force to export" << endl;	
	
	FtE = FieldToExport(Th,sigma11[],sigma22[],sigma12[]);		
	
	// ================================================================================
	// Outputs ========================================================================
	
	cout << " " << endl;
	cout << "    Generating outputs" << endl;

	PlotToFreeFEM(i+1, Nitr, errordNR, erroruNR);

	ExportToVTUfile(Th,d[],u1[],eps11[],eps22[],eps12[],sigma11[],sigma22[],sigma12[],i);

	tsolve= clock()-tsolve;		
	Totaltime = Totaltime + tsolve;
	ExportToFile(i);

	// ================================================================================
	// Updating fields ================================================================

	cout << " " << endl;
	cout << "    Updating fields" << endl;
	
	Hold = H;	
	
}	

cout << " ===================================================================================================== " << endl;
cout << " ====                                          CPU time                                          ===== " << endl;
cout << " ===================================================================================================== " << endl;

cout << " ALL solving steps :::: "  << clock()-time << endl;

cout << " ===================================================================================================== " << endl;
cout << " ===================================================================================================== " << endl;
cout << " "  << endl;
cout << " "  << endl;
cout << " "  << endl;
cout << " "  << endl;
